import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager.Foundations;
using Azure.Core;
using Azure.ResourceManager;

namespace Microsoft.AzureSphere;

interface Operations extends Azure.ResourceManager.Operations {}

// Common models
@lroStatus
enum ProvisioningState {
  ...ResourceProvisioningState,
  Provisioning,
  Updating,
  Deleting,
  Accepted,
}

@doc("Regional data boundary values.")
enum RegionalDataBoundary {
  None,
  EU
}

@doc("Allow crash dumps values.")
enum AllowCrashDumpCollection {
  Enabled,
  Disabled
}

@doc("Response to the action call for count devices in a catalog.")
model CountDeviceResponse extends CountElementsResponse {}

@doc("Response of the count for elements.")
model CountElementsResponse {
  @doc("Number of devices in product.")
  value: integer
}

@doc("Parameters for paginated APIs")
model ListQueryParameters {
  ...FilterQueryParameter;
  ...StandardListQueryParameters;
}

// Templates
interface ProxyResourceOperationsReadList<
    TResource extends ArmResource,
    TListParameters extends object = {}>
  extends Azure.ResourceManager.ResourceRead<TResource>,
    ResourceListByParent<TResource, TListParameters & BaseParameters<TResource>> {}

interface ProxyResourceOperationsReadListCreateDelete<
    TResource extends ArmResource,
    TListParameters extends object = {}>
  extends Azure.ResourceManager.ResourceRead<TResource>,
    ResourceListByParent<TResource, TListParameters & BaseParameters<TResource>>,
    ResourceCreate<TResource>,
    Azure.ResourceManager.ResourceDelete<TResource>{}