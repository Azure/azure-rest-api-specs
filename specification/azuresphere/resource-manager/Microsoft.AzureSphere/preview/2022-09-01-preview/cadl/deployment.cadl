import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager.Foundations;
using Azure.Core;
using Azure.ResourceManager;

namespace Microsoft.AzureSphere;

// Deployment
@doc("An deployment resource belonging to a device group resource.")
@parentResource(DeviceGroup)
model Deployment is ProxyResource<DeploymentProperties> {
  @doc("Deployment name.")
  @key("deploymentName")
  @path
  @segment("deployments")
  name: string;
}

@armResourceOperations
interface Deployments extends ProxyResourceOperationsReadListCreateDelete<Deployment, ListQueryParameters> {}

@doc("The properties of deployment")
model DeploymentProperties {
  @doc("Images deployed")
  @visibility("create", "read")
  deployedImages?: Image[];

  @doc("Deployment date UTC")
  @visibility("read")
  deploymentDateUtc?: zonedDateTime;

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;
}