import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager.Foundations;
using Azure.Core;
using Azure.ResourceManager;

namespace Microsoft.AzureSphere;

//Catalogs
@doc("An Azure Sphere catalog")
model Catalog is TrackedResource<CatalogProperties> {
  @doc("Name of catalog")
  @pattern("^[A-Za-z0-9_-]{1,50}$")
  @key("catalogName")
  @path
  @segment("catalogs")
  name: string;
}

@doc("Catalog properties")
model CatalogProperties {

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;
}

@doc("Request of the action to list device groups for a catalog.")
model ListDeviceGroupsRequest {
  @doc("Device Group name.")
  deviceGroupName?: string;
}
@doc("Device insight report.")
model DeviceInsight {

  @doc("Device ID")
  deviceId: string;

  @doc("Event description")
  description: string;

  @doc("Event start timestamp")
  startTimestampUtc: zonedDateTime;
  
  @doc("Event end timestamp")
  endTimestampUtc: zonedDateTime;

  @doc("Event category")
  eventCategory: string;

  @doc("Event class")
  eventClass: string;

  @doc("Event type")
  eventType: string;

  @doc("Event count")
  eventCount: int32;
}

@armResourceOperations
interface Catalogs extends Azure.ResourceManager.ResourceOperations<Catalog, CatalogProperties> {

  @autoRoute
  @doc("Counts devices in catalog.")
  @armResourceAction(Catalog)
  @post
  op countDevices(
    ...ResourceInstanceParameters<Catalog>,
  ): ArmResponse<CountDeviceResponse> | ErrorResponse;

  @autoRoute
  @doc("Lists device insights for catalog.")
  @armResourceAction(Catalog)
  @post
  op listDeviceInsights(
    ...ResourceInstanceParameters<Catalog>,
    ...ListQueryParameters
  ): ArmResponse<Page<DeviceInsight>> | ErrorResponse;

  @autoRoute
  @doc("Lists devices for catalog.")
  @armResourceAction(Catalog)
  @post
  op listDevices(
    ...ResourceInstanceParameters<Catalog>,
    ...ListQueryParameters
  ): ArmResponse<ResourceListResult<Device>> | ErrorResponse;

  @autoRoute
  @doc("Lists deployments for catalog.")
  @armResourceAction(Catalog)
  @post
  op listDeployments(
    ...ResourceInstanceParameters<Catalog>,
    ...ListQueryParameters
  ): ArmResponse<ResourceListResult<Deployment>> | ErrorResponse;

  @autoRoute
  @armResourceAction(Catalog)
  @doc("List the device groups for the catalog.")
  @post
  op listDeviceGroups(
    ...ResourceInstanceParameters<Catalog>,
    ...ListQueryParameters,
    @doc("List device groups for catalog.")
    @body listDeviceGroupsRequest: ListDeviceGroupsRequest
  ): ArmResponse<ResourceListResult<DeviceGroup>> | ErrorResponse;
}



