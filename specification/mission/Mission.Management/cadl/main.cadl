import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

import "./virtualenclave.cadl";

@armNamespace
@serviceTitle("Microsoft Mission")
@serviceVersion("2022-12-07-preview")
@doc("Microsoft Mission Resource Provider management API.")
namespace Microsoft.Mission;

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.Core;
using Azure.ResourceManager;


@doc("Community Resource Properties")
model communityProperties{
  @doc("Community Description.")
  description?: string;
  
  @doc("Address Space.")
  addressSpace?: string;

  @doc("Dns Servers.")
  dnsServers?: string[];
  
  @doc("Provisioning State.")
  @visibility("read")
  provisioningState?: provisioningState;
}

@doc("Virtual Enclave Resource properties")
model virtualEnclaveProperties{
  @doc("Virtual Enclave Resource Group Id.")
  enclaveResourcesGroupId?: string;

  @doc("Virtual Enclave Virtual Network.")
  enclaveVirtualNetwork?: enclaveVirtualNetwork;

  @doc("Virtual Enclave Core.")
  enclaveCore?: enclaveCore;

  @doc("Virtual Enclave Administrators Object Id.")
  enclaveAdministratorsObjectId?: string;

  @doc("Provisioning State.")
  @visibility("read")
  provisioningState?: provisioningState;

  @doc("Community Resource Id.")
  communityResourceId: string;

  @doc("Community Resource Object.")
  @visibility("read")
  communityResource?: communityResource;
}

@doc("Virtual Enclave Model Resource")
model enclaveResource is TrackedResource<virtualEnclaveProperties> {
  @key("virtualEnclaveName")
  @segment("virtualEnclaves")
  @doc("The name of the enclaveResource Resource")
  @path  
  name: string;
}

@doc("Community Model Resource")
model communityResource is TrackedResource<communityProperties> {
  @key("communityName")
  @segment("communities")
  @path
  @doc("The name of the communityResource Resource")
  name: string;
}

@doc("Virtual Enclave Interface")
@armResourceOperations
interface VirtualEnclave extends ResourceOperations<enclaveResource, virtualEnclaveProperties> {
}

@doc("Community Interface")
@armResourceOperations
interface Community extends ResourceOperations<communityResource, communityProperties> {
}