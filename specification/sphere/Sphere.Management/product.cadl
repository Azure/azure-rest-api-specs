import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager.Foundations;
using Azure.Core;
using Azure.ResourceManager;

namespace Microsoft.AzureSphere;

//Products
@doc("An product resource belonging to a catalog resource.")
@parentResource(Catalog)
model Product is ProxyResource<ProductProperties> {
  @doc("Name of product.")
  @pattern("^[\\w][\\w\\s]{1,48}[\\w]$|^\\.default$|^\\.unassigned$")
  @key("productName")
  @path
  @segment("products")
  name: string;
}

interface ProxyResourceOperationsProducts<
    TResource extends ArmResource,
    TListParameters extends object = {}>
    extends ResourceListByParent<TResource, TListParameters & BaseParameters<TResource>>{
        @doc("Get a Product. '.default' and '.unassigned' are system defined values and cannot be used for product name.")
        get is ArmResourceRead<Product>;
        @doc("Create a Product. '.default' and '.unassigned' are system defined values and cannot be used for product name.")
        createOrUpdate is ArmResourceCreateOrUpdateAsync<Product>;
        @doc("Delete a Product. '.default' and '.unassigned' are system defined values and cannot be used for product name'")
        delete is ArmResourceDeleteAsync<Product>;
        @doc("Update a Product. '.default' and '.unassigned' are system defined values and cannot be used for product name.")
        update is ArmResourcePatchAsync<Product, ProductProperties>;
    }

@armResourceOperations
interface Products extends ProxyResourceOperationsProducts<Product> {

  @autoRoute
  @doc("Generates default device groups for the product. '.default' and '.unassigned' are system defined values and cannot be used for product name.")
  @armResourceAction(Product)
  @post
  op generateDefaultDeviceGroups(
    ...ResourceInstanceParameters<Product>,
  ): ArmResponse<ResourceListResult<DeviceGroup>> | ErrorResponse;

  @autoRoute
  @doc("Counts devices in product. '.default' and '.unassigned' are system defined values and cannot be used for product name.")
  @armResourceAction(Product)
  @post
  op countDevices(
    ...ResourceInstanceParameters<Product>,
  ): ArmResponse<CountDeviceResponse> | ErrorResponse;
}

@doc("The properties of product")
model ProductProperties {
  @doc("Description of the product")
  description: string;

  @visibility("read")
  @doc("The status of the last operation.")
  provisioningState?: ProvisioningState;
}