// Definitions for the OperatorVoicemailInstance resource

import "@cadl-lang/openapi";
import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-autorest";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using Azure.ResourceManager;

@armProviderNamespace("Microsoft.OperatorVoicemail")
@service({
  title: "Microsoft.OperatorVoicemail",
})
@versioned(Versions)
@versionedDependency(
  [
    [
      Versions.v2023_03_01_preview,
      Azure.ResourceManager.Versions.v1_0_Preview_1
    ]
  ]
)

namespace Microsoft.OperatorVoicemail;

enum Versions {
  v2023_02_01_preview: "2000-01-01-preview",
  v2023_03_01_preview: "2023-03-01-preview"
}

interface Operations extends Azure.ResourceManager.Operations {}

// Various tooling enforces (or at least suggests) that all models should have a provisioning State field, as this means something special to ARM.

@doc("Provisioning state of the resource.")
enum ProvisioningState {
    ...ResourceProvisioningState
}

@doc("General resource properties.")
model ResourceProperties {
  @doc("Resource provisioning state.")
  @visibility("read")
  provisioningState?: ProvisioningState;
}

// A helper template for defining empty properties for resource PATCH operations
// In a future release this won't be necessary.
// See https://github.com/Azure/azure-rest-api-specs-pr/pull/8707#discussion_r971363996 for context
@doc("The type used for update operations of the {name}.", TResource)
@friendlyName("{name}Update", TResource)
model ResourceUpdateModelNoProperties<TResource extends object>
  is OptionalProperties<UpdateableProperties<OmitProperties<
    TResource,
    "Name" | "name"
  >>>;

#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-interface-requires-decorator" "Interface template does not require this."
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-interface-uses-templates" "This is an official operations template."
@doc("Template for resources with no updateable properties")
@armResourceOperations
interface ResourceUpdateNoProperties<TResource extends Foundations.ArmResource> {
  @autoRoute
  @doc("Update a {name}", TResource)
  @armResourceUpdate(TResource)
  @patch
  update(
    ...ResourceInstanceParameters<TResource>,

    @doc("The resource properties to be updated.")
    @body
    properties: ResourceUpdateModelNoProperties<TResource>
  ): ArmResponse<TResource> | ErrorResponse;
}

// These models should be provided by the common CADL infra, but currently they are not. See GitHub PR:
// https://github.com/Azure/cadl-azure/pull/2311
// For now, these definitions are reimplementations of the common types from:
// https://github.com/Azure/azure-rest-api-specs-pr/blob/66dc2c9a0243bc217a938583a53da052498ac39f/specification/common-types/resource-management/v3/types.json

// Ideally all of these would be marked as @added(Versions.v2022_12_02_preview) but they can't be because of
// https://github.com/microsoft/cadl/issues/1332

@doc("The check availability request body.")
model CheckNameAvailabilityRequest {
  @doc("The name of the resource for which availability needs to be checked.")
  name?: string;

  @doc("The resource type.")
  type?: string;
}

@doc("Possible reasons for a name not being available.")
enum CheckNameAvailabilityReason {
  "Invalid",
  "AlreadyExists",
}

@doc("The check availability result.")
model CheckNameAvailabilityResponse {
  @doc("Indicates if the resource name is available.")
  nameAvailable?: boolean;

  @doc("The reason why the given name is not available.")
  reason?: CheckNameAvailabilityReason;

  @doc("Detailed reason why the given name is not available.")
  message?: string;
}

@doc("Available auto-generated domain name scopes.")
enum AutoGeneratedDomainNameLabelScope {
    TenantReuse,
    SubscriptionReuse,
    ResourceGroupReuse,
    NoReuse
}

@doc("A OperatorVoicemailInstance resource")
model OperatorVoicemailInstance is TrackedResource<OperatorVoicemailInstanceProperties> {
  @pattern("^[a-zA-Z][a-zA-Z0-9-]{2,23}$")
  @key("operatorVoicemailInstanceName")
  @segment("operatorVoicemailInstances")
  @doc("Unique identifier for this Operator Voicemail deployment")
  @visibility("Read")
  @path
  name: string;

  ...ResourceSku;
}

@doc("Details of SIP trunk.")
model OperatorVoicemailVoipConnection {
  @doc("Domain name of the peer connecting to Azure Operator Voicemail over this SIP trunk. This must match the common name of the TLS certificate presented by the peer.")
  @visibility("read", "update", "create")
  remoteDomain: string;

  @doc("The allowed source IP address or CIDR ranges for signaling")
  @visibility("read", "update", "create")
  allowedSignalingSourceAddressPrefixes: string[];

  @doc("The allowed source IP address or CIDR ranges for media")
  @visibility("read", "update", "create")
  allowedMediaSourceAddressPrefixes: string[];
}

@doc("Details of the OperatorVoicemailInstance resource.")
model OperatorVoicemailInstanceProperties is ResourceProperties {
  @doc("The set of SIP trunks to be configured on this deployment")
  @OpenAPI.extension("x-ms-identifiers", ["remoteDomain"])
  @visibility("read", "update", "create")
  @minItems(1)
  voipConnections: OperatorVoicemailVoipConnection[];

  @added(Versions.v2023_03_01_preview)
  @doc("The scope at which the auto-generated domain name can be re-used")
  @visibility("read", "create")
  autoGeneratedDomainNameLabelScope?: AutoGeneratedDomainNameLabelScope = AutoGeneratedDomainNameLabelScope.TenantReuse;

  @added(Versions.v2023_03_01_preview)
  @doc("The autogenerated label used as part of the FQDNs for accessing the Azure Operator Voicemail")
  @visibility("read")
  autoGeneratedDomainNameLabel?: string;
}

@added(Versions.v2023_03_01_preview)
@armResourceOperations
interface OperatorVoicemailInstances
  extends ResourceRead<OperatorVoicemailInstance>,
    ResourceCreate<OperatorVoicemailInstance>,
    ResourceDelete<OperatorVoicemailInstance>,
    ResourceListBySubscription<OperatorVoicemailInstance>,
    ResourceListByParent<OperatorVoicemailInstance>,
    ResourceUpdateNoProperties<OperatorVoicemailInstance> {}

#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-interface-requires-decorator" "CheckNameAvailability is not supported by cadl-azure-resource-manager."
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-interface-uses-templates" "CheckNameAvailability is not supported by cadl-azure-resource-manager."
#suppress "@azure-tools/cadl-azure-resource-manager/arm-resource-operation-missing-decorator" "CheckNameAvailability is not supported by cadl-azure-resource-manager."
@added(Versions.v2023_03_01_preview)
interface NameAvailability {
  @doc("Check whether the resource name is available in the given region.")
  @post
  @route("/subscriptions/{subscriptionId}/providers/Microsoft.OperatorVoicemail/locations/{location}/checkNameAvailability")
  checkLocal(
    @doc("The check availability request body.")
    @body
    body: CheckNameAvailabilityRequest,

    @doc("The location in which uniqueness will be verified.")
    @path
    location: string,

    ...SubscriptionIdParameter,
    ...ApiVersionParameter
  ): CheckNameAvailabilityResponse | ErrorResponse;
}