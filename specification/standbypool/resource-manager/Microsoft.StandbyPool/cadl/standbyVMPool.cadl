// -----------------------------------------------------------------------
// <copyright file="standbyVMPool.cadl" company="Microsoft Corporation">
// Copyright (c) Microsoft Corporation. All rights reserved.
// </copyright>
// -----------------------------------------------------------------------

import "@cadl-lang/rest";
import "@cadl-lang/versioning";
import "@azure-tools/cadl-providerhub";
import "@azure-tools/cadl-azure-core";
import "@azure-tools/cadl-azure-resource-manager";

using Azure.Core;
using Azure.ResourceManager;
using Cadl.Http;
using Cadl.Rest;
using Cadl.Versioning;
using OpenAPI;

@armProviderNamespace("Microsoft.StandbyPool")
@service({
  title: "Microsoft.StandbyPool",
  version: "2023-06-01-preview",
})
@useDependency(Azure.ResourceManager.Versions.v1_0_Preview_1)
namespace Microsoft.StandbyPool;

interface Operations extends Azure.ResourceManager.Operations {}

@doc("A StandbyVirtualMachinePoolResource.")
model StandbyVirtualMachinePoolResource is TrackedResource<StandbyVirtualMachinePoolResourceProperties> {
  @pattern("^[a-zA-Z0-9-]{3,24}$")
  @key("standbyVirtualMachinePoolName")
  @segment("standbyVirtualMachinePools")
  @visibility("read")
  @doc("Name of the standby virtual machine pool")
  @path
  name: string;
}

@doc("Details of the StandbyVirtualMachinePool.")
model StandbyVirtualMachinePoolResourceProperties {
  @doc("Specifies maximum number of standby VMs. When the pool is attached to VMScaleSets, the limit applies to each VMScaleSet.")
  @maxValue(2000)
  maxReadyCapacity: int64;

  @doc("Specifies list of VirtualMachineStateMixPolicy.")
  @extension("x-ms-identifiers", ["vmState"])
  virtualMachineStateMixPolicies: VirtualMachineStatePolicy[];

  @doc("Specifies list of VMScaleSets' fully qualified resource IDs the pool is attached to. When multiple VMScaleSets are specified, standby VMs are created for each VMScaleSet independently.")
  attachedVirtualMachineScaleSetIds?: string[];

  @doc("The status of the last operation.")
  @visibility("read")
  provisioningState?: ProvisioningState;
}

@doc("Details of VirtualMachineStatePolicy")
model VirtualMachineStatePolicy {
  @doc("State of standby virtual machines")
  vmState: VirtualMachineState;

  @doc("Percentage number of standby VMs in this state.")
  @minValue(0)
  @maxValue(100)
  percentage: int32;
}

@doc("State of standby virtual machines")
enum VirtualMachineState {
  Running,
  Stopped,
}

@doc("Provisioning state")
@lroStatus
enum ProvisioningState {
  ...ResourceProvisioningState,
  Provisioning,
  Updating,
  Deleting,
}

@armResourceOperations
interface StandbyVirtualMachinePool {
  get is ArmResourceRead<StandbyVirtualMachinePoolResource>;
  createOrUpdate is ArmResourceCreateOrUpdateAsync<StandbyVirtualMachinePoolResource>;
  delete is ArmResourceDeleteAsync<StandbyVirtualMachinePoolResource>;
  update is ArmResourcePatchSync<StandbyVirtualMachinePoolResource, StandbyVirtualMachinePoolResourceProperties>;
  listByResourceGroup is ArmResourceListByParent<StandbyVirtualMachinePoolResource>;
  listBySubscription is ArmListBySubscription<StandbyVirtualMachinePoolResource>;
}