import "@azure-tools/cadl-azure-core";
import "@cadl-lang/rest";
import "./models.cadl";

using Cadl.Rest;
using Cadl.Http;

namespace MultivariateAnomalyDetector;

@summary("Get Multivariate Anomaly Detection Result")
@doc("""
For asynchronous inference, get multivariate anomaly detection result based on
resultId returned by the BatchDetectAnomaly api.
""")
op GetBatchDetectionResult is Azure.Core.ResourceRead<DetectionResult>;

@summary("Train a Multivariate Anomaly Detection Model")
@doc("""
Create and train a multivariate anomaly detection model. The request must
include a source parameter to indicate an externally accessible Azure blob
storage URI.There are two types of data input: An URI pointed to an Azure blob
storage folder which contains multiple CSV files, and each CSV file contains
two columns, timestamp and variable. Another type of input is an URI pointed to
a CSV file in Azure blob storage, which contains all the variables and a
timestamp column.
""")
op CreateMultivariateModel is Azure.Core.ResourceCreateWithServiceProvidedName<Model>;

@summary("List Multivariate Models")
@doc("List models of a resource.")
op ListMultivariateModel is Azure.Core.ResourceList<
  Model,
  {
    parameters: {
      @doc("Skip indicates how many models will be skipped.")
      @query
      skip: int32;

      @doc("Top indicates how many models will be fetched.")
      @query
      top: int32;
    };
  }
>;

@summary("Delete Multivariate Model")
@doc("Delete an existing multivariate model according to the modelId")
@route("/multivariate/models/{modelId}")
@delete
op DeleteMultivariateModel is Azure.Core.Foundations.Operation<
  {
    @doc("Model identifier")
    @path
    modelId: string;
  },
  void
>;

@summary("Get Multivariate Model")
@doc("""
Get detailed information of multivariate model, including the training status
and variables used in the model.
""")
op GetMultivariateModel is Azure.Core.ResourceRead<Model>;

@summary("Detect Multivariate Anomaly")
@route(":batch-detect")
@doc("""
Submit multivariate anomaly detection task with the modelId of trained model
and inference data, the input schema should be the same with the training
request. The request will complete asynchronously and return a resultId to
query the detection result.The request should be a source link to indicate an
externally accessible Azure storage Uri, either pointed to an Azure blob
storage folder, or pointed to a CSV file in Azure blob storage.
""")
op BatchDetectAnomaly is Azure.Core.Foundations.Operation<{}, DetectionResult>;

@summary("Detect anomalies in the last point of the request body")
@route(":detect-last")
@doc("""
Submit multivariate anomaly detection task with the modelId of trained model
and inference data, and the inference data should be put into request body in a
JSON format. The request will complete synchronously and return the detection
immediately in the response body.
""")
op LastDetectAnomaly is Azure.Core.Foundations.Operation<{}, LastDetectionResult>;
