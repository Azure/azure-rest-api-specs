namespace Microsoft.AzureSphereV2;

using Azure.ResourceManager;
using Cadl.Http;
using Cadl.Rest;

@doc("An Azure Sphere provisioning package.")
@parentResource(Catalog)
model ProvisioningPackage is TrackedResource<ProvisioningPackageProperties> {
  @doc("Name of the provisioning package")
  @key("provisioningPackageName")
  @segment("provisioningPackages")
  @visibility("read")
  @path()
  name: string;
}

@doc("The properties of ProvisioningPackage.")
model ProvisioningPackageProperties is ResourceProperties {
  @doc("Proof of possession challenge value for device provisioning.")
  @visibility("read")
  challenge?: string;

  @doc("Base endpoint for data plane operations including devices to request a certificate with the attestation measurements and others.")
  @visibility("read")
  baseEndpoint?: string;

  @doc("Expiration date for his bulk device registration.")
  @visibility("read", "write")
  expirationDate?: zonedDateTime;
}

@armResourceOperations
interface ProvisioningPackages extends ResourceOperations<ProvisioningPackage, ProvisioningPackageProperties> {
  @post
  @doc("Returns the formatted provisioning package.")
  @segment("format")
  op Format(
    ...ResourceInstanceParameters<ProvisioningPackage>
  ): ArmResponse<ProvisioningPackageFormatActionResponse> | ErrorResponse;
}

// Action types:

@doc("Parameters of the format provisioning package request.")
model ProvisioningPackageFormatRequest {
}

@doc("Values of the format provisioning package response.")
model ProvisioningPackageFormatActionResponse {
    @doc("Provisioning package resource identifier.")
    id: string;
    @doc("Name of the provisioning package.")
    name: string;
    @doc("Version of the provisioning package format.")
    version: string;
    @doc("Properties of the provisioning package")
    properties: {
        @doc("Name of the catalog where the devices will be provisioned and activated.")
        catalogName: string;
        @doc("The location (region) of the catalog.")
        location: string;
        @doc("Base of the endpoint of the catalog used in the device for the different AS3 services including attestation, device insights and others.")
        baseEndpoint: string;
        @doc("Unique random GUID proof of possession challenge value used during provisioning and activation stages to validate the possession of the private key in the device.")
        challenge: string;
        @doc("An UTC creation date of the provisioning package.")
        createdDate: zonedDateTime;
        @doc("An UTC expiration date of the provisioning package defined by the Catalog owner during provisioning package creation. New devices cannot be activated in the catalog after the expiration date.")
        expirationDate: zonedDateTime;
    }
}
